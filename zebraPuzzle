#maxint = 5.

nazione(spagna).
nazione(inghilterra).
nazione(norvegia).
nazione(russia).
nazione(giappone).

ordine(1,X) | ordine(2,X) | ordine(3,X) | ordine(4,X) | ordine(5,X) :- nazione(X).
:-ordine(Y,X), ordine(Z,X), Y != Z.
:-ordine(X,Y), ordine(X,Z), Y != Z.
:-not ordine(1,norvegia).

vive(X,verde) | vive(X,avorio) | vive(X,blu) | vive(X,giallo) | vive(X,rossa) :- nazione(X).
:-vive(Y,X), vive(Z,X), Y != Z.
:-vive(X,Y), vive(X,Z), Y != Z.
:-ordine(2,X), vive(X,Y), Y!=blu.
:-not vive(inghilterra,rossa).

vicino(X,Y) :- ordine(Z,A), ordine(Q,B), vive(A,X), vive(B,Y), Q=Z+1.
:-not vicino(avorio,verde).

padrone(X,cane) | padrone(X,cavallo) | padrone(X,lumaca) | padrone(X,volpe) | padrone(X,zebra) :- nazione(X).
:-padrone(Y,X), padrone(Z,X), Y != Z.
:-padrone(X,Y), padrone(X,Z), Y != Z.
:-not padrone(spagna,cane).

fuma(X,winston) | fuma(X,luckystrike) | fuma(X,chesterfields) | fuma(X,parliaments) | fuma(X,kools) :- nazione(X).
:-fuma(Y,X), fuma(Z,X), Y != Z.
:-fuma(X,Y), fuma(X,Z), Y != Z.
:- not fuma(giappone,parliaments).
:- fuma(X,chesterfields), vive(X,Y), vive(Q,Z), padrone(Q,volpe), not vicino(Y,Z), not vicino(Z,Y).
:- fuma(X,kools), vive(X,Y), vive(Q,Z), padrone(Q,cavallo), not vicino(Y,Z), not vicino(Z,Y).
:- fuma(X,kools), not vive(X,giallo).
:- padrone(X,lumaca), not fuma(X,winston).

beve(X,acqua) | beve(X,caffe) | beve(X,teh) | beve(X,latte) | beve(X,aranciata) :- nazione(X).
:-beve(Y,X), beve(Z,X), Y != Z.
:-beve(X,Y), beve(X,Z), Y != Z.
:-not beve(russia,teh).
:-beve(X,aranciata), not fuma(X,luckystrike).
:-beve(X,caffe), not vive(X,verde).
:-beve(X,latte), ordine(Y,X), Y != 3.

zebrapuzzle(X,W,Y,Z,Q,P) :- ordine(X,Y), vive(Y,Z), padrone(Y,Q), fuma(Y,W), beve(Y,P).
